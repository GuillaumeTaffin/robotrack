import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.springframework.boot' version '3.0.1'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.graalvm.buildtools.native' version '0.9.18'
    id 'org.jetbrains.kotlin.jvm' version '1.7.22'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.7.22'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.9.0'
}

def restAssuredVersion = '5.3.0'

group = 'com.gt'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

ext {
    set('testcontainersVersion', "1.17.6")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'io.projectreactor.kotlin:reactor-kotlin-extensions'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-reactor'
    implementation 'org.springframework:spring-jdbc'
    implementation("org.springdoc:springdoc-openapi-starter-webflux-ui:2.0.2")
    implementation("org.springdoc:springdoc-openapi-starter-common:2.0.2")

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'org.postgresql:r2dbc-postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:r2dbc'
    testImplementation "io.rest-assured:rest-assured:$restAssuredVersion"
    testImplementation "io.rest-assured:spring-web-test-client:$restAssuredVersion"
    testImplementation "io.rest-assured:kotlin-extensions:$restAssuredVersion"
}

pitest {
    junit5PluginVersion = '1.1.1'
    excludedClasses = [
            'com.gt.robotrack.RobotrackApplicationKt',
    ]
    withHistory = true
    avoidCallsTo = [
            'kotlin.jvm.internal',
            'org.slf4j',
            'java.util.logging',
            'org.apache.log4j',
            'org.apache.commons.logging'
    ]
    coverageThreshold = 100
    mutationThreshold = 100
}


jacoco {
    toolVersion = "0.8.8"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'SOURCEFILE'
            excludes = [
                    '**RobotrackApplication**',
            ]

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
        }
        rule {
            element = 'SOURCEFILE'
            excludes = [
                    '**RobotrackApplication**',
            ]

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
        }
        rule {
            element = 'METHOD'
            excludes = [
                    '**RobotrackApplication**',
            ]

            limit {
                counter = 'COMPLEXITY'
                value = 'TOTALCOUNT'
                maximum = 1.0
            }
        }
    }
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: '**/RobotrackApplicationKt**')
        }))
    }
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '17'
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.register("ciPipeline") {
    dependsOn tasks.build
    dependsOn tasks.jacocoTestReport
    dependsOn tasks.jacocoTestCoverageVerification
    dependsOn tasks.pitest
    tasks.jacocoTestCoverageVerification.mustRunAfter tasks.jacocoTestReport
}
